buildscript {
    ext {
        kotlin_version = '1.2.41'
        shadow_version = '2.0.4'
        kovenant_version = '3.3.0'
        vertx_version = '3.5.1'
        slf4j_version = '1.7.25'
        fuel_version = '1.13.0'
        moshi_version = '1.6.0'
        kotson_version = '2.5.0'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version",
                "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
    }
}

group = 'ec.solmedia.kotlin.vertx.app'
version = '1.0-SNAPSHOT'

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'kotlin-kapt'

repositories {
    jcenter()
}

dependencies {
    // kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Kovenant: Promises for Kotlin
    compile "nl.komponents.kovenant:kovenant:$kovenant_version"

    // vertex and slf4j
    compile "io.vertx:vertx-core:$vertx_version"
    runtime "org.slf4j:slf4j-jdk14:$slf4j_version"
    compile "org.slf4j:slf4j-api:$slf4j_version"
    // vertx router
    compile "io.vertx:vertx-web:$vertx_version"
    // Themeleaf: Rendering templates
    compile "io.vertx:vertx-web-templ-thymeleaf:$vertx_version"

    // Fuel: The easiest HTTP networking library for Kotlin/Android
    compile "com.github.kittinunf.fuel:fuel:$fuel_version"

    // Moshi: Modern JSON library with kotlin codegen
    compile "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    // Kotson: Syntactic sugars to Gson in Kotlin
    compile "com.github.salomonbrys.kotson:kotson:$kotson_version"
}

mainClassName = "io.vertx.core.Launcher"
def mainVerticleName = 'verticles.MainVerticle'
def watchForChange = 'src/**/*.kt'
def doOnChange = 'gradle classes'

run {
    args = ['run', mainVerticleName, "--redeploy=$watchForChange",
            "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}